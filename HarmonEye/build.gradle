apply plugin: 'java'
apply plugin: 'eclipse'

version = "1.0-${new Date().format('yyyyMMdd-HHmmss')}"

repositories {
	mavenCentral()
	flatDir {
		dirs 'lib'
	}
}


configurations {
	bundleapp
}

sourceSets {
	experiments
}

dependencies {
	compile 'colt:colt:1.2.0'
	compile 'edu.emory.mathcs:JTransforms:2.4'
	compile 'org.apache.commons:commons-math3:3.1.1'
	compile 'org.apache.commons:commons-lang3:3.1'

	compile 'org.simplericity.macify:macify:1.6'

	bundleapp ':appbundler:1.0'
	
	compile ':jogl-all:'
	compile ':jogl-all-natives-macosx-universal:'
	compile ':gluegen-rt:'
	compile ':gluegen-rt-natives-macosx-universal:'
	
	compile ':jna:3.5.1'
	compile ':turboactivate:1.0'
	
  	testCompile 'junit:junit:4.10'
}

jar {
	manifest {
		attributes 'Main-Class': 'com.harmoneye.HarmonEye'
	}
	from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}


ant {
	taskdef(
		name: 'bundleapp',
		classname: 'com.oracle.appbundler.AppBundlerTask',
		classpath: configurations.bundleapp.asPath
	)
}

task bundleapp(dependsOn: 'jar') << {
	def distDirPath = "dist"
	def distDir = new File(distDirPath)
	distDir.mkdirs()
    ant.bundleapp(
    	outputdirectory: distDirPath,
    	name: "HarmonEye",
    	displayname: "HarmonEye",
    	identifier: "com.harmoneye.HarmonEye",
    	mainclassname: "com.harmoneye.HarmonEye",
    	shortversion: version,
    	applicationCategory: "public.app-category.music",
      icon: "src/main/resources/HarmonEye.icns"
    ) {
    	classpath(file: jar.archivePath)
    }
	ant.copy(toDir: distDirPath + "/HarmonEye.app/Contents/Java/TurboActivate") {
		fileset(dir: "lib/TurboActivate")
	}
}
